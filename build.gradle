apply plugin: 'java'
apply plugin: 'maven-publish'

def ENV = System.getenv()

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 8

version = "${version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = project.maven_group
archivesBaseName = project.archives_base_name

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.21.0.1'
    implementation 'mysql:mysql-connector-java:8.0.13'
    implementation 'org.postgresql:postgresql:42.2.23.jre7'

    // Logging
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.archives_base_name,
                "Specification-Vendor": "twpi",
                "Specification-Version": "1",
                "Implementation-Title": project.archives_base_name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"twpi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
	repositories {
        if(ENV.MAVEN_REPO_USER && ENV.MAVEN_REPO_SECRET) {
            // Releases for CI builds
            maven {
                url "https://maven.rmnad.net/releases"
                credentials {
                    username = "${ENV.MAVEN_REPO_USER}"
                    password = "${ENV.MAVEN_REPO_SECRET}"
                }
            }
        } else {
            // Snapshot builds for local testing
            maven {
                url "https://maven.rmnad.net/snapshots"
                credentials {
                    username = MAVEN_REPO_USER
                    password = MAVEN_REPO_SECRET
                }
            }
        }
	}
}

java {
    withSourcesJar()
}